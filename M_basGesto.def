Option Compare Database
Option Explicit

Public Function Salir()
    On Error GoTo HandleError
    Dim sFormName As String, sReportName As String, iR As Integer
    If Not Screen.ActiveForm Is Nothing Then
        sFormName = Screen.ActiveForm.Name
    ElseIf Not Screen.ActiveReport Is Nothing Then
        sReportName = Screen.ActiveReport.Name
    End If
    If sFormName <> "" Then
        If sFormName = "Inicio" Then
            iR = MsgBox("Cerrar Programa", vbQuestion + vbOKCancel, "Gesto")
            If iR = vbOK Then DoCmd.Quit
            GoTo HandleExit
        End If
        DoCmd.Close acForm, sFormName
        GoTo HandleExit
    End If
    If sReportName <> "" Then
        DoCmd.Close acReport, sReportName
    End If
            
HandleExit:
    Exit Function
HandleError:
    MsgBox Err.Description
    Resume HandleExit
End Function

Public Function RowSourceMeses(Optional bIncluirTodos As Boolean = True) As String
    RowSourceMeses = IIf(bIncluirTodos, "0;Todos;", "") _
                    & "1;Enero;" _
                    & "2;Febrero;" _
                    & "3;Marzo;" _
                    & "4;Abril;" _
                    & "5;Mayo;" _
                    & "6;Junio;" _
                    & "7;Julio;" _
                    & "8;Agosto;" _
                    & "9;Septiembre;" _
                    & "10;Octubre;" _
                    & "11;Noviembre;" _
                    & "12;Diciembre"
End Function

Public Sub PonTotalFactura(lngNroFactura As Long)
    On Error GoTo Error_PonTotalFactura
    Dim strSQL As String, curTotal As Currency, curBaseRet As Currency
    curTotal = Nz(DSum("[Precio]*(1+[%IVA]/100)", "Facturas lineas", "[Nro Documento] = " & lngNroFactura), 0)
    curTotal = Round(curTotal, 2)
    curBaseRet = Nz(DSum("[Precio]", "Facturas lineas", "[Nro Documento] = " & lngNroFactura & " AND [%IVA]<>0"), 0)
    curBaseRet = Round(curBaseRet, 2)
    strSQL = "UPDATE [Facturas cabeceras] SET [Facturas cabeceras].TotalImporte = " & ComaPunto(curTotal)
    strSQL = strSQL & ", BaseRetencionIRPF = " & ComaPunto(curBaseRet)
    strSQL = strSQL & " WHERE ((([Facturas cabeceras].[Nro Documento])=" & lngNroFactura & "));"
    CurrentDb.Execute strSQL, dbFailOnError + dbSeeChanges
Salir_PonTotalFactura:
    Exit Sub
Error_PonTotalFactura:
    Select Case Err
        Case Else
            MsgBox "Error nº " & Err & " en PonTotalFactura" & vbCrLf & Err.Description
            Resume Salir_PonTotalFactura
    End Select
End Sub